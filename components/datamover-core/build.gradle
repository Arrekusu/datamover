archivesBaseName = "datamover-core"

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

sourceCompatibility = 1.7

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'org.hamcrest', name: 'hamcrest-all', version: '1.3'
    testCompile(group: 'junit', name: 'junit', version: '4.11') {
        exclude group: 'org.hamcrest', module: 'hamcrest-core';
    }
    testCompile group: 'com.shazam', name: 'shazamcrest', version: '0.7'
    testCompile(group: 'org.mockito', name: 'mockito-all', version: '1.9.5') {
        exclude group: 'org.hamcrest', module: 'hamcrest-core';
    }
    compile group: 'org.springframework', name: 'spring-jdbc', version: '4.0.7.RELEASE'
    compile group: 'org.slf4j', name: 'log4j-over-slf4j', version: '1.7.7'
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.7'
    compile group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.5'
    compile group: 'com.google.guava', name: 'guava', version: '17.0'
    compile group: 'com.sun.xml.bind', name: 'jaxb-impl', version: '2.2.4'

}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    required { isReleaseVersion && gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'DataMover'
                packaging 'jar'
                description 'Simple framework for business data migration between applications.'
                url 'https://github.com/Arrekusu/DataMover'

                scm {
                    connection 'scm:git:git@github.com:Arrekusu/DataMover.git'
                    developerConnection 'scm:git:git@github.com:Arrekusu/DataMover.git'
                    url 'scm:git:git@github.com:Arrekusu/DataMover.git'
                }

                licenses {
                    license {
                        name 'MIT License'
                        url 'http://www.opensource.org/licenses/mit-license.php'
                    }
                }

                developers {
                    developer {
                        id 'Arekusu'
                        name 'Aleksandr Ananev'
                        email 'arrekusu.a@gmail.com'
                    }
                }
            }
        }
    }
}